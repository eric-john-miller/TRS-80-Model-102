100 REM CTCPL.BA
110 REM Crisps Tunes Compiler
120 REM Compiles crisps tunes ABC
130 REM music notation files into
140 REM playable frequency and
150 REM duration for TRS-80 Model 102
160 REM BASIC sound command code files
170 REM For the BASIC Month 5 challenge
180 REM Crisps Tunes
190 REM http://redit.com/r
200 REM /RetroBattlestations
210 REM written by FozzTexx
220 REM Modifications----------------
230 REM 2020.07.17 Add "T:" notation 
240 REM 2020.07.22 Add JBOX call
290 REM -------------------------------
300 PRINT "Crisps Tunes Compiler"
310 PRINT "r/retrobattlestations"
320 PRINT "writen by u/SDGOL"
330 PRINT "based on original code by FozzTexx"
400 REM Initialize Variables
410 MAXFILES=2
420 LN=0
430 DIM M(7)
440 FOR I=1 TO 7
450 READ M(I)
460 NEXT I
470 DIM HZ(72)
480 FOR I=1 TO 72
490 READ HZ(I)
500 NEXT I
510 SH=0:RH=0:OM=4
520 DIM A(12*OM)
530 A1=1:A2=12*OM
540 GOSUB 3000
600 INPUT "ABC File:";FN$
610 IF LEN(FN$) > 4 THEN PRINT "File name must be four chars or shorter":GOTO 9999
615 PRINT "Writing CT"+FN$
620 OPEN "RAM:"+FN$ FOR INPUT AS 1
630 OPEN "RAM:CT"+FN$ FOR OUTPUT AS 2
700 REM read file and compile song
710 IF EOF(1) GOTO 9999
720 LINE INPUT #1,S$
730 SL=LEN(S$)
740 IF SL=0 THEN GOTO 9999
750 IF LEFT$(S$,2)="T:" THEN LN=LN+10:PRINT #2,LN;"PRINT "+CHR$(34)+RIGHT$(S$,LEN(S$)-2)+CHR$(34):GOTO 710
760 FOR I=1 TO SL
770 OC=4:DI=0:MU=0:AC=0
780 IF I>SL THEN GOTO 1170
790 N$=MID$(S$,I,1)
800 IF N$=" " THEN GOTO 1170
810 IF N$="!" THEN GOSUB 3000:GOTO 1170
820 IF N$="^" THEN AC=1:SH=SH+1:I=I+1:GOTO 780
830 IF N$="-" THEN AC=1:SH=SH-1:I=I+1:GOTO 780
840 IF N$="=" THEN AC=1:SH=0:I=I+1:GOTO 780
850 IF N$>="a" AND N$<="g" THEN N$=CHR$(ASC(N$))-32:OC=OC+1
860 IF N$>="A" AND N$<="G" THEN C=ASC(N$):C=C-64:NT=M(C)
870 REM Keep reading modifier after the
890 REM note until there's another note
900 I=I+1:IF I>SL THEN GOTO 1010
910 M$=MID$(S$,I,1)
920 IF M$="," THEN OC=OC-1:GOTO 900
930 IF M$="'" THEN OC=OC+1:GOTO 900
940 IF M$>="0" AND M$<="9" THEN C=ASC(M$):MU=MU*10+C-48:GOTO 900
950 IF M$="/" THEN DI=1:GOTO 900
960 IF M$="<" THEN RH=1:RM=0.5:GOTO 900
970 IF M$=">" THEN RH=1:RM=1.5:GOTO 900
990 I=I-1
1000 REM NOTE PROPERTIES
1010 IF NT<0 THEN NT=NT+12:OC=OC-1:GOTO 1010
1020 IF AC THEN AI=NT+13:A(AI)=SH:SH=0
1030 IF AC AND AI<A1 THEN A1=AI
1040 IF AC AND AI<A2 THEN A2=AI
1050 SH=A(NT+13):NT=NT+SH
1060 IF DI AND MU=0 THEN MU=2
1070 IF DI THEN MU=1/MU
1080 IF MU=0 THEN MU=1
1090 IF RH THEN MU=MU*RM
1100 DU=INT(32/MU)
1110 F$="":IF SH<>0 THEN F$="#":IF SH<0 THEN F$="$"
1120 PRINT N$;F$;
1130 REM Note (NT) is numeric note,
1140 REM 0 is middle C.
1150 GOSUB 2000
1160 IF RH THEN RH=RH+1:RM=2-RM:IF RH=3 THEN RH=0
1170 NEXT I
1180 GOTO 710
2000 REM SUB OUTPUT
2010 J=NT+1+(12*(OC-1))
2020 FQ=HZ(J)
2030 LN=LN+10
2040 PRINT #2,LN;"SOUND"+STR$(FQ)+","+STR$(DU)
2050 RETURN
3000 REM clear all accidentals
3010 FOR AD=A1 TO A2:A(AD)=0:NEXT AD
3020 A1=12*OM:A2=0
3030 RETURN
9000 DATA 9,11,0,2,4,5,7
9010 REM musical scale frequencies
9020 DATA 0,0,0,15800,14912,14064
9030 DATA 13284,12538,11836,11172
9040 DATA 10544,9952
9050 DATA 9394,8866,8368,7900,7457
9060 DATA 7032,6642,6269,5918,5586
9070 DATA 5272,4968
9080 DATA 4697,4433,4184,3950,3728
9090 DATA 3516,3321,3134,2954,2793
9100 DATA 2636,2484
9110 DATA 2348,2216,2092,1975,1864
9120 DATA 1758,1660,1567,1479,1396
9130 DATA 1318,1224
9140 DATA 1171,1103,1045,987,932,873
9150 DATA 830,783,739,693,659,622
9160 DATA 587,554,523,493,466,439,415
9170 DATA 0,0,0,0,0
9999 LN=LN+10:PRINT #2,LN;"RUN "+CHR$(34)+"JBOX.BA"+CHR$(34):CLOSE:END
